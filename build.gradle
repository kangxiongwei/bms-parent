// 所有子项目的通用配置
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'

    version = '1.0-SNAPSHOT'

    // JVM 版本号要求
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // java编译的时候缺省状态下会因为中文字符而失败
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    //定义版本号
    ext {
        springVersion = '4.3.3.RELEASE'
        hibernateVersion = '5.2.2.Final'
    }

    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'http://mvnrepository.com/' }
        mavenCentral()
    }

    jar {
        manifest {
            attributes("Implementation-Title": "Gradle")
        }
    }

    configurations {
        // 所有需要忽略的包定义在此
        all*.exclude group: 'commons-httpclient'
        all*.exclude group: 'commons-logging'
        all*.exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }

    dependencies {
        // 通用依赖
        compile(
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-orm:$springVersion",
                "org.springframework:spring-tx:$springVersion",
                "org.springframework.data:spring-data-jpa:1.10.3.RELEASE",
                "org.hibernate:hibernate-entitymanager:$hibernateVersion",
                "c3p0:c3p0:0.9.1.2",
                "mysql:mysql-connector-java:6.0.4",
                "org.slf4j:slf4j-nop:1.7.21",
                "commons-fileupload:commons-fileupload:1.3.2",
                "com.fasterxml.jackson.core:jackson-databind:2.8.2"
        )

        // 依赖maven中不存在的jar
        ext.jarTree = fileTree(dir: 'libs', include: '**/*.jar')
        ext.rootProjectLibs = new File(rootProject.rootDir, 'libs').getAbsolutePath()
        ext.jarTree += fileTree(dir: rootProjectLibs, include: '**/*.jar')

        compile jarTree

        // 测试依赖
        testCompile(
                "org.springframework:spring-test:$springVersion",
                "junit:junit:4.12"
        )
    }

    // 显示当前项目下所有用于 compile 的 jar.
    task listJars(description: 'Display all compile jars.') << {
        configurations.compile.each { File file -> println file.name }
    }
}

jar {
    baseName = 'bms-parent'
    version = '0.0.1-SNAPSHOT'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

//把JAR复制到目标目录
task release(type: Copy) {
    from('build/libs') {
        include '*.jar'
    }
    from('deploy') {
        include '*.sh'
        include '*.yml'
    }
    into('target')
}

dependencies {

    compile project(':bms-commons')
    compile project(':bms-core')
    compile project(':bms-order')
    compile project(':bms-stat')

    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.0'

    //thrift依赖
    compile 'org.apache.thrift:libthrift:0.10.0'


    testCompile group: 'junit', name: 'junit', version: '4.12'
}
